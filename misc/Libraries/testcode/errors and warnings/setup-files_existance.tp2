// Never Forget: Comments can "lie", but code not so much ;P
BACKUP ~./weidu_external/files_existance/backup~ // %MOD_FOLDER% doesn't work here
AUTHOR ~YOU~
VERSION ~v0.0.0~
//AUTO_EVAL_STRINGS// This will prevent the need of EVAL for string variables


// --------------
// Initialization
// --------------
ALWAYS 
	// LIBRARY INCLUDES(Don't change these)
	// ------------------------------------
	INCLUDE ~%MOD_FOLDER%/lib/warnings.tph~
	
	// CREATING ITEM FILE(for testing patch version)
	// ----------------------------------------------
	CREATE ~ITM~ ~Testfile~
	
END// End of "ALWAYS"


LANGUAGE
	~English~	// Language
	~English~	// Directory
	~%MOD_FOLDER%/en_US/!_warnings.tra~
	
LANGUAGE
	~Deutsch~	// Language
	~German~	// Directory
	~%MOD_FOLDER%/de_DE/!_warnings.tra~


// -----------
// Start Tests
// -----------
BEGIN ~Files existance~

// Creates a file array
ACTION_DEFINE_ARRAY files 
BEGIN 
	~dlc/sod-dlc.zip~ 
	~sod-dlc.zip~ 
END

// OUTSIDE OF PATCHING
// -------------------
OUTER_SPRINT warning @2147483644

// Checks all files
OUTER_FOR (index = 0; VARIABLE_IS_SET $files(~%index%~); ++index) BEGIN

	// Gets specific file
	OUTER_TEXT_SPRINT file $files(~%index%~)

	// Calls warning 
	LAF WARN_EVALUATE_FILE_EXISTANCE 
		STR_VAR 
			file 	= EVAL ~%file%~
			message = EVAL ~%warning%~
		END
END

// PATCHING ITEM TESTFILE
// ----------------------
COPY_EXISTING ~Testfile.ITM~ ~%MOD_FOLDER%~
SPRINT warning @2147483644

// Checks all files
FOR (index = 0; VARIABLE_IS_SET $files(~%index%~); ++index) BEGIN

	// Gets specific file
	TEXT_SPRINT file $files(~%index%~)

	// Calls warning 
	LPF WARN_EVALUATE_FILE_EXISTANCE 
		STR_VAR 
			file 	= EVAL ~%file%~
			message = EVAL ~%warning%~
		END
END